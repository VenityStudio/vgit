plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

group = 'org.venity'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-jdbc'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-starter-web'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile 'mysql:mysql-connector-java'
    compile 'io.jsonwebtoken:jjwt:0.9.0'
    compile 'javax.xml.bind:jaxb-api:2.3.0'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
}

task npmInstall(type: Exec) {
    workingDir 'src/main/frontend'
    
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        commandLine 'cmd', '/c', 'npm', 'install'
    } else {
        commandLine 'npm', 'install'
    }
}

task npmBuild(type: Exec, dependsOn: npmInstall) {
    workingDir 'src/main/frontend'

    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        commandLine 'cmd', '/c', 'npm', 'run-script', 'build'
    } else {
        commandLine 'npm', 'run-script', 'build'
    }
}

task runWithFrontend(type: GradleBuild) {
    tasks = ['npmBuild', 'bootRun']
}

task buildWithFrontend(type: GradleBuild) {
    tasks = ['npmBuild', 'bootJar']
}
